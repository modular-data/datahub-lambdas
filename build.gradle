plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '7.1.1' // originally 7.1.2 but downgraded since circle CI build was failing
    id 'org.sonarqube'  version '3.5.0.2730'
    id 'org.owasp.dependencycheck'  version '8.2.1'
    id 'org.gradle.java-test-fixtures'
    id 'org.barfuin.gradle.jacocolog' version '3.1.0'
}

group 'uk.gov.justice'

version "${version != 'unspecified' ? version : '0.0.13-SNAPSHOT'}"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jacoco {
    toolVersion = "0.8.8" // Adjust the version if needed
}


repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.1'
    hamcrestVersion = '2.2'
    mockitoVersion = '5.2.0'
    amazonSdkVersion = '1.12.590'
    amazonRedShiftSdkVersion = '2.26.22'
    lambdaCoreVersion = '1.2.3'
    lambdaJavaEventsVersion = '3.11.3'
    systemLambdaVersion = '1.2.1'
}

dependencies {
    implementation "com.amazonaws:aws-java-sdk-s3:$amazonSdkVersion"
    implementation "com.amazonaws:aws-java-sdk-dynamodb:$amazonSdkVersion"
    implementation "com.amazonaws:aws-java-sdk-stepfunctions:$amazonSdkVersion"
    implementation "com.amazonaws:aws-java-sdk-glue:$amazonSdkVersion"
    implementation "com.amazonaws:aws-lambda-java-core:$lambdaCoreVersion"
    implementation "com.amazonaws:aws-lambda-java-events:$lambdaJavaEventsVersion"
    implementation "software.amazon.awssdk:redshiftdata:$amazonRedShiftSdkVersion"

    testImplementation "com.github.stefanbirkner:system-lambda:$systemLambdaVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    // https://mvnrepository.com/artifact/org.mockito/mockito-inline
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

sourceSets {
    integrationTest {
        java.srcDir 'src/it/java'
        resources.srcDir 'src/it/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation, testImplementation
}

dependencies {
    integrationTestImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    integrationTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

// Configure the integrationTest task
tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'

    maxHeapSize = '1024m'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    binaryResultsDirectory = file("$buildDir/integration-test-results/binary/integrationTest")

    testLogging {
        events "passed", "skipped", "failed"
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

    reports {
        html.outputLocation = file("$buildDir/reports/integration-test")
        junitXml.outputLocation = file("$buildDir/integration-test-results")
    }

    mustRunAfter tasks.test
}

// Configure the check task to depend on integrationTest
check.dependsOn integrationTest

jacocoTestReport {
    dependsOn test
    dependsOn integrationTest
    reports {
        xml.required = false
        html.required = true
    }
}

shadowJar {
    zip64 true
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
        "-Xlint:all"
    ]
}


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
    testLogging {
        events "passed", "skipped", "failed"
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    println("\nUsing $maxParallelForks executors")
}

assemble {
  dependsOn shadowJar
}
